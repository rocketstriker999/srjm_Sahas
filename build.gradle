plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.hammerbyte'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //spring boot starters
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
     implementation 'org.springframework.boot:spring-boot-starter-security'

    // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt
    implementation 'io.jsonwebtoken:jjwt:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    //mysql connector
    runtimeOnly 'com.mysql:mysql-connector-j'

    //spring boot dev tools to restart project
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    //spring boot test starters
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

//Junit5
tasks.named('test') {
    useJUnitPlatform()
}

//React Project Build 
task buildReact(type: Exec) {
    commandLine 'sh', '-c', 'npm run build'
}

// Define clean task to delete 'web-app' folder
task cleanReact(type: Delete) {
    delete 'package-lock.json'
    delete 'src/main/resources/web-app'
}

task cleanJava(type: Delete) {
    delete 'bin'
}

tasks.named('clean') {
    dependsOn cleanJava,cleanReact
}

tasks.named('processResources') {
    dependsOn buildReact
}

//to build react project and java both run ./gradlew clean build

//start app
//  ./gradlew bootRun